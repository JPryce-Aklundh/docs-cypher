:description: The `MATCH` clause is used to search for the pattern described in it.
include::https://raw.githubusercontent.com/neo4j-graphacademy/courses/main/asciidoc/ads/data-analysis.adoc[]

= MATCH

The `MATCH` clause allows you to specify the patterns Neo4j will search for in the database.

[[match-example-graph]]
== Example graph

The following graph is used for the examples below:

image::graph_match_clause.svg[width="600",role="middle"]

To recreate the graph, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE (charlie:Person {name: 'Charlie Sheen'}),
       (martin:Person {name: 'Martin Sheen'}),
       (michael:Person {name: 'Michael Douglas'}),
       (oliver:Person {name: 'Oliver Stone'}),
       (rob:Person {name: 'Rob Reiner'}),
       (wallStreet:Movie {title: 'Wall Street'}),
       (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
       (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
       (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
       (oliver)-[:DIRECTED]->(wallStreet),
       (thePresident:Movie {title: 'The American President'}),
       (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
       (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
       (rob)-[:DIRECTED]->(thePresident),
       (martin)-[:FATHER_OF]->(charlie)
----

[[find-nodes]]
== Find nodes

For more information about finding node patterns, see xref:patterns/fixed-length-patterns#node-patterns[Patterns -> Node patterns].


[[find-all-nodes]]
=== Find all nodes

By specifying a pattern with a single node and no labels, all nodes in the graph will be returned.

.Find all nodes in a graph
[source, cypher]
----
MATCH (n)
RETURN n
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| n
| (:Person {"name":"Charlie Sheen"})
| (:Person {"name":"Martin Sheen"})
| (:Person {"name":"Michael Douglas"})
| (:Person {"name":"Oliver Stone"})
| (:Person {"name":"Rob Reiner"})
| (:Movie {"title":"Wall Street"})
| (:Movie {"title":"The American President"})
|Rows: 7
|===


[[find-nodes-with-label]]
=== Find nodes with a specific label

.Find all nodes with the `Movie` label
[source, cypher]
----
MATCH (movie:Movie)
RETURN movie.title
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| movie.title
| "Wall Street"
| "The American President"
|Rows: 2
|===


[[match-label-expressions]]
=== MATCH using label expressions

.Node pattern using the `OR` (`|`) label expression
[source, cypher]
----
MATCH (n:Movie|Person)
RETURN n.name AS name, n.title AS title
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | title
| "Charlie Sheen" | <null>
| "Martin Sheen" | <null>
| "Michael Douglas" | <null>
| "Oliver Stone" | <null>
| "Rob Reiner" | <null>
| <null> | "Wall Street"
| <null> | "The American President"
2+|Rows: 7
|===

For a full list of all label expressions supported by Cypher, see xref:patterns/reference.adoc#label-expressions[Patterns -> Label expressions].

[[find-relationships]]
== Find relationships

Relationships will only be matched once inside a single pattern.
Read more about this behavior in the section on xref::patterns/reference.adoc#graph-patterns-rules-relationship-uniqueness[relationship uniqueness].

For more information about relationship patterns, see xref:patterns/fixed-length-patterns#relationship patterns[Patterns -> Relationship patterns].

[[empty-relationship-patterns]]
=== Empty relationship patterns

By applying `--`, a pattern will be matched for a relationship with any direction and without any filtering on relationship types or properties.
When matching a relationship pattern, a node pattern `()` must be applied at both ends.

.Find connected nodes using an empty relationship pattern
----
MATCH (director {name: 'Oliver Stone'})--(n)
RETURN n
----

.Result
[source, role="queryresult",options="header,footer",cols="1*<m"]
|===
| n
| (:Movie {title: "Wall Street"})
|===

[[directed-relationship-patterns]]
=== Directed relationship patterns

The direction of a relationship in a pattern is indicated by arrows: `-->` or `<--`.

.Find all nodes connected to `Oliver Stone` by an outgoing relationship.
[source, cypher]
----
MATCH (:Person {name: 'Oliver Stone'})-->(movie)
RETURN movie.title
----

.Result
[source, role="queryresult",options="header,footer",cols="1*<m"]
|===
| movie.title
| "Wall Street"
|Rows: 1
|===

[[relationship-variables]]
=== Relationship variables

It is possible to introduce a variable to a pattern, either for filtering on relationship properties or to return a relationship.

.Find the types of an aliased relationship
[source, cypher]
----
MATCH (:Person {name: 'Oliver Stone'})-[r]->(movie)
RETURN type(r)
----

.Result
[source, role="queryresult",options="header,footer",cols="1*<m"]
|===
| type(r)
| "DIRECTED"
|Rows: 1
|===

[[undirected-relationships]]
=== MATCH on an undirected relationship

When a pattern contains a bound relationship, and that relationship pattern does not specify direction, Cypher will match the relationship in both directions.

.Relationship pattern without direction
[source, cypher]
----
MATCH (a)-[:ACTED_IN {role: 'Bud Fox'}]-(b)
RETURN a, b
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| a | b

| (:Movie {"title":"Wall Street"})
| (:Person {"name":"Charlie Sheen"})

| (:Person {"name":"Charlie Sheen"})
| (:Movie {"title":"Wall Street"})

2+|Rows: 2
|===

[[match-on-relationship-type]]
=== Filter on relationship types

When the relationship type to match on is known, it is possible to specify it by using a colon (`:`) before the relationship type.

.Relationship pattern filtering on a relationship type
[source, cypher]
----
MATCH (wallstreet:Movie {title: 'Wall Street'})<-[:ACTED_IN]-(actor)
RETURN actor.name
----

.Result
[source, role="queryresult",options="header,footer",cols="1*<m"]
|===
| actor.name
| "Michael Douglas"
| "Martin Sheen"
| "Charlie Sheen"
|Rows: 3
|===

[[match-on-multiple-relationship-types]]
=== Filter on multiple relationship types

It is possible to match for a pattern containing one out of several relationship types using the `OR` symbol, `|`.

.Relationship pattern including either `ACTED_IN` or `DIRECTED` relationship types
[source, cypher]
----
MATCH (wallstreet {title: 'Wall Street'})<-[:ACTED_IN|DIRECTED]-(person)
RETURN person.name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| person.name
| "Oliver Stone"
| "Michael Douglas"
| "Martin Sheen"
| "Charlie Sheen"
|Rows: 4
|===

[[multiple-rels]]
=== Find multiple relationships

A graph pattern can contain several relationship patterns.

.Graph pattern including several relationship patterns
[source, cypher]
----
MATCH (charlie {name: 'Charlie Sheen'})-[:ACTED_IN]->(movie)<-[:DIRECTED]-(director)
RETURN movie.title, director.name
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| movie.title | director.name
| "Wall Street" | "Oliver Stone"
2+|Rows: 1
|===

[[match-where-predicates]]
== MATCH with WHERE predicates

The MATCH clause is often paired with a `WHERE` sub-clause, which adds predicates to refine the patterns, making them more specific.
These predicates are part of the pattern itself, not just filters applied after matching.
Thus, the `WHERE` clause should always be placed with its corresponding `MATCH` clause.

.Basic containing `WHERE` predicate
[source, cypher]
----
MATCH (charlie:Person)-[:ACTED_IN]->(movie:Movie)
WHERE charlie.name = 'Charlie Sheen'
RETURN movie.title
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| movie.title
| "Wall Street"
|Rows: 1
|===

.More complex `WHERE` predicate
[source, cypher]
----
MATCH (martin:Person)-[:ACTED_IN]->(movie:Movie)
WHERE martin.name = 'Martin Sheen' AND NOT EXISTS {
    MATCH (movie)<-[:DIRECTED]-(director:Person {name: 'Oliver Stone'})
}
RETURN movie.title
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| movie.title
| "The American President"
|Rows: 1
|===

For more information, see the xref:clauses/where.adoc[`WHERE`] page.

[[find-paths]]
== Find paths

The `MATCH` clause can also be used to bind paths to variables.

.Find all paths matching a pattern
[source, cypher]
----
MATCH path = ()-[:ACTED_IN]->(movie:Movie)
RETURN path
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| path
| (:Person {name: "Charlie Sheen"})-[:ACTED_IN {role: "Bud Fox"}]->(:Movie {title: "Wall Street"}) 
| (:Person {name: "Martin Sheen"})-[:ACTED_IN {role: "Carl Fox"}]->(:Movie {title: "Wall Street"})
| (:Person {name: "Martin Sheen"})-[:ACTED_IN {role: "A.J. MacInerney"}]->(:Movie {title: "The American President"})
| (:Person {name: "Michael Douglas"})-[:ACTED_IN {role: "Gordon Gekko"}]->(:Movie {title: "Wall Street"})
| (:Person {name: "Michael Douglas"})-[:ACTED_IN {role: "President Andrew Shepherd"}]->(:Movie {title: "The American President"})
| Rows: 5
|===


.Find paths matching a pattern including a `WHERE` predicate
[source, cypher]
----
MATCH path = (:Person)-[:ACTED_IN]->(movie:Movie)<-[:DIRECTED]-(:Person)
WHERE movie.title = 'Wall Street'
RETURN path
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| path
| (:Person {name: "Charlie Sheen"})-[:ACTED_IN {role: "Bud Fox"}]->(:Movie {title: "Wall Street"})<-[:DIRECTED]-(:Person {name: "Oliver Stone"})
| (:Person {name: "Martin Sheen"})-[:ACTED_IN {role: "Carl Fox"}]->(:Movie {title: "Wall Street"})<-[:DIRECTED]-(:Person {name: "Oliver Stone"})
| (:Person {name: "Michael Douglas"})-[:ACTED_IN {role: "Gordon Gekko"}]->(:Movie {title: "Wall Street"})<-[:DIRECTED]-(:Person {name: "Oliver Stone"})
|Rows: 3
|===

For more information about how `MATCH` is used to find patterns (including xref:patterns/variable-length-patterns.adoc#quantified-path-patterns[quantified path patterns], xref:patterns/variable-length-patterns.adoc#quantified-relationships[quantified relationships], and xref:patterns/shortest-paths.adoc[shortest paths]), see the section on xref::patterns/index.adoc[Patterns].

== WITH and using multiple MATCH clauses

MATCH (martin:Person {name: 'Martin Sheen'})-[:ACTED_IN]->(movie:Movie)
WITH movie  // Pass the movie variable to the next part of the query
MATCH (director:Person)-[:DIRECTED]->(movie)
RETURN director.name AS Director, movie.title AS MovieTitle


== MATCH with parameters

:param movieTitle => 'Wall Street';
 MATCH (movie:Movie {title: $movieTitle})

